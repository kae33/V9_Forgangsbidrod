;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Honnunarskjal fyrir eininguna "forgangsbidrod" ;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; 
;;; Innflutt (Imported):
;;;  Notkun: b = erAUndan(x,y)
;;;  Fyrir:  x og y eru gildi af theirri gerd sem vid
;;;          viljum bera saman
;;;  Eftir:  b er satt ef x verdur ad vera a undan y, 
;;;          osatt ef y verdur ad vera a undan x.
;;;
;;;  Notkun: y = insert(x,u)
;;;  Fyrir:  x er listi para af theirri gerd sem <<==
;;;          raedur vid hausinn, i vaxandi rod midad vid <<==, 
;;;          u er einnig par af theirri gerd.
;;;  Eftir:  y er listi i vaxandi rod midad vid <<==
;;;          sem inniheldur oll gildin ur x auk u.
;;;
;;; Innflutt (Exported):
;;;  Notkun: f = buaTil()
;;;  Eftir:  f er forgangsbidrod
;;; 
;;;  Notkun: b = erTom(f)
;;;  Fyrir:  f er forgangbidrod
;;;  Eftir:  b er satt th.th.a.a. forgangsbidrod f == null
;;; 
;;;  Notkun: f = baetaI(f,g,k)
;;;  Fyrir:  f er forgangbidrod
;;;  Eftir:  forgangsbidrod f ad vidbaettu gildi g a 
;;;          stad i forgangsbidrod mida vid forgang <<==.
;;;
;;;  Notkun: g = saekja(f)
;;;  Fyrir:  f er forgangbidrod
;;;  Eftir:  gildi g med lykil k sem hefur haesta forgang,
;;;          forgangsbidrodin an gildisins og lykli þess.
;;;
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Honnunarskjali lykur her ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

"forgangsbidrod.mmod" = 
!
{{
;;; Fastayrding gagna
;;; Forgangsbidrod f er taknud sem bendir a breytu 
;;; sem inniheldur lista af pörum [[k1$g1], [k2$g2], ..., [kN$gN]] 
;;; theirra lykla k og gilda g sem eru i forgangsbidrodinni. Thar sem 
;;; k1 verdur ad vera a undan k2 o.s.frv.
buaTil =
	fun()
	{
		var l = [], p;
        p = &l;
        return p;
	};

erTom =
    fun(f)
    {
        if ( *f==null ){
            return true;
        } else {
            return false;
        }
    };
baetaI =
    fun(f,g,k)
    {
        *f = insert(*f,g:k);
        return f;
    };
saekja = 
    fun(f)
    {
        val res = tail(head(*f));
        *f = tail(*f);
        return res;
    };

}}
*
!
{{
insert =
	fun(x,u)
	{
		x==[] && (return [u]);
		head(head(x)) <<== head(u) || (return u:x);
		head(x) : insert(tail(x),u);
	};
}}
*
{{
<<== = fun erAUndan(x,y);
}}
;

show "forgangsbidrod.mmod";

"test1.mexe" = main in
{{
main =
	fun()
	{

        var f = buaTil();
        
		var x=[1,9,2,8,3,7,4,6,5];
		while( x!=[] )
		{
            ;;; Fastayrding lykkju:
            ;;; samanlagt innihalda listinn x
            ;;; og forgangsbidrodinrod f tolurnar ur 
            ;;; pokanum {1,2,3,4,5,6,7,8,9}
            f = baetaI(f,head(x),head(x));
			x = tail(x);
		};

        while( erTom(f)==false ) {
            ;;; Fastayrding lykkju:
            ;;; Samanlagt er forgangsbidrodin og 
            ;;; tolurnar sem eru skrifadar ut
            ;;; pokinn {1,2,3,4,5,6,7,8,9}
		    write(saekja(f)++" ");
        };
        writeln();

	};
}}
*
"forgangsbidrod.mmod"
*
{{
erAUndan = fun <(x,y);
}}
*
BASIS
;

"test2.mexe" = main in
{{
main =
	fun()
	{

        var f = buaTil();
        
		var x=[1,9,2,8,3,7,4,6,5];
		while( x!=[] )
		{
            ;;; Fastayrding lykkju:
            ;;; samanlagt innihalda listinn x
            ;;; og forgangsbidrodinrod f tolurnar ur 
            ;;; pokanum {1,2,3,4,5,6,7,8,9}
            f = baetaI(f,head(x),head(x));
			x = tail(x);
		};

        while( erTom(f)==false ) {
            ;;; Fastayrding lykkju:
            ;;; Samanlagt er forgangsbidrodin og 
            ;;; tolurnar sem eru skrifadar ut
            ;;; pokinn {1,2,3,4,5,6,7,8,9}
		    write(saekja(f)++" ");
        };
        writeln();
	};
}}
*
"forgangsbidrod.mmod"
*
{{
erAUndan = fun >(x,y);
}}
*
BASIS
;
